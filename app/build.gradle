apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("dev-keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

def stagingEnvironmentPropertiesFile = rootProject.file("staging-environment.properties")
def stagingEnvironmentProperties = new Properties()
stagingEnvironmentProperties.load(new FileInputStream(stagingEnvironmentPropertiesFile))

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "lv.spkc.gov.apturicovid"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 56
        versionName "1.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "SAFETY_NET_API_KEY", secretsProperties["SAFETY_NET_API_KEY"])
        buildConfigField("String", "SECRET_KEY", secretsProperties["SECRET_KEY"])
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            testCoverageEnabled = true
        }
    }

    productFlavors {
        staging {
            applicationIdSuffix ".staging"

            buildConfigField("String", "API_DOMAIN", stagingEnvironmentProperties["API_DOMAIN"])
            buildConfigField("String", "FILES_DOMAIN", stagingEnvironmentProperties["FILES_DOMAIN"])
            buildConfigField("String[]", "API_SSL_PINS", "new String[]{" + stagingEnvironmentProperties["API_SSL_PINS"] + "}")
            buildConfigField("String[]", "FILES_SSL_PINS", "new String[]{" + stagingEnvironmentProperties["FILES_SSL_PINS"] + "}")
        }
        production {
            buildConfigField("String", "API_DOMAIN", "\"apturicovid-api.spkc.gov.lv\"")
            buildConfigField("String", "FILES_DOMAIN", "\"apturicovid-files.spkc.gov.lv\"")
            buildConfigField("String[]", "API_SSL_PINS", "new String[]{" +
                    "\"sha256/AD1268BbF/w+Hh5JXyPLDLVAdh67VIG2agjkdYjC/kI=\"," +
                    "\"sha256/KhxI+AM5BFntDid1jc3tGAWOh/Qg8uG4ARPFF/QdXyg=\"," +
                    "\"sha256/3LJ/K6jSPOrKBxIZSUFxI0dmVpKPGj0a5WVm7/RPh8w=\"," +
                    "\"sha256/Mr5krFQTCZzw9aBSWDcRTiX6pD3qEpBytrjXwKt7/v8=\"}")
            buildConfigField("String[]", "FILES_SSL_PINS", "new String[]{" +
                    "\"sha256/u4OJJH+SZhvigfdKR+e1F3/OmICvFTnUl5Fnv6myaHg=\"," +
                    "\"sha256/OMEJX6dz1nbHkHom4UjDI6IV7MntvN2OyaV4xHsdRdA=\"," +
                    "\"sha256/dgbAjpXEfPJsPkFbQx99pUUg7iXQgCsjSNTs1A/X8mI=\"," +
                    "\"sha256/gVxo9q/ofaPErOFjlBfYGUlX4RPMYwFIcpCPKMWX3aM=\"}")
        }
    }

    flavorDimensions "default"

    signingConfigs {
        debug {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    api 'com.google.android.gms:play-services-base:17.5.0'
    api fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    def daggerVersion = '2.25.2'
    def recyclerviewVersion = '1.1.0'
    def constraintLayoutVersion = '2.0.4'
    def workVersion = "2.5.0-beta02"
    def glideVersion = "4.11.0"
    def glideCompilerVersion = "4.10.0"
    def glideOkHttpIntegrationVersion = "4.11.0"
    def guavaVersion = '27.0.1'
    def coroutinesVersion = "1.3.9"
    def retrofitVersion = '2.8.1'
    def jodaTimeVersion = '2.10.2'
    def timberTreeUtilsVersion = '1.0.0'
    def timberVersion = '4.7.1'
    def ok2curlVersion = "0.6.0"
    def lifecycleVersion = "2.2.0"
    def roomVersion = '2.2.5'
    def navigationVersion = '2.3.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.navigation:navigation-fragment:$navigationVersion"
    implementation "androidx.navigation:navigation-ui:$navigationVersion"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.security:security-crypto:1.1.0-alpha02"

    implementation "com.google.android.gms:play-services-safetynet:$google_play_services_version"

    //test
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutinesVersion"

    // android support libraries
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.google.android:flexbox:2.0.1'

    //work manager
    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$workVersion"

    // glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation ("com.github.bumptech.glide:okhttp3-integration:$glideOkHttpIntegrationVersion"){
        exclude group: 'glide-parent'
    }
    kapt "com.github.bumptech.glide:compiler:$glideCompilerVersion"

    // core libraries for java
    implementation "com.google.guava:guava:$guavaVersion-android"

    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.0'

    // joda time
    api "net.danlew:android.joda:$jodaTimeVersion"
    testImplementation "joda-time:joda-time:$jodaTimeVersion"

    // pin edit text
    implementation ('com.alimuzaffar.lib:pinentryedittext:2.0.6') {
        // Exclude is only needed if you already have
        // androidx.appcompat as a dependency in your app.
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-auth-api-phone:17.5.0'
    implementation 'com.google.android.gms:play-services-basement:17.5.0'
    implementation 'com.google.android.gms:play-services-tasks:17.2.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.0'

    // logger'
    implementation "net.ypresto.timbertreeutils:timbertreeutils:$timberTreeUtilsVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.github.mrmike:ok2curl:$ok2curlVersion"

    // room
    api "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"
}
